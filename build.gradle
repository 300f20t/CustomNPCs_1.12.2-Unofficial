buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "org.spongepowered.mixin"

version = getVersion()
group = 'ego.betazavr.nirn'
archivesBaseName = "Nirn-${mc_version}-Unofficial"

static def getVersion() {
	Date date = new Date()
	String month
	switch(date.getMonth()) {
		case 1: month = "Feb"; break
		case 2: month = "Mar"; break
		case 3: month = "Apr"; break
		case 4: month = "May"; break
		case 5: month = "Jun"; break
		case 6: month = "Jul"; break
		case 7: month = "Aug"; break
		case 8: month = "Sep"; break
		case 9: month = "Oct"; break
		case 10: month = "Nov"; break
		case 11: month = "Dec"; break
		default: month = "Jan"; break
	}
    return "_v.3.001_" + date.format("dd") + month + date.format("yyyy") + '-Snapshot'
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    accessTransformer = file('src/main/resources/META-INF/deobfuscation.cfg')

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'noppes.npcs.mixin.NpcsLaunchPlugin,zone.rong.mixinbooter.MixinBooterPlugin'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            property "mixin.debug", 'true'
            arg "-mixin.config=mixins.npcs.json"
            arg '--username=BetaZavr'
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'noppes.npcs.mixin.NpcsLaunchPlugin,zone.rong.mixinbooter.MixinBooterPlugin'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            property "mixin.debug", 'true'
            arg "-mixin.config=mixins.npcs.json"
            arg 'nogui'
        }
    }
}

repositories {
    // maven { url "https://www.cursemaven.com" }
    maven { url 'https://maven.cleanroommc.com' }
    flatDir { dirs 'lib' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Configuration lib
    // compile "curse.maven:configuration-1086839:${configuration_version}"

    // Mixins
    compile fg.deobf('zone.rong:mixinbooter:9.3')
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
	
	compile name: 'geckolib-forge-1.12.2-3.0.31-deobf'
}

sourceSets {
    main {
        ext.refMap = "mixins.npcs.refmap.json"
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"        : "CustomNPCs",
                "Specification-Vendor"       : "BetaZavr",
                "Specification-Version"      : "1",
                "Implementation-Title"       : project.name,
                "Implementation-Version"     : "${version}",
                "Implementation-Vendor"      : "BetaZavr",
                "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin"              : "noppes.npcs.mixin.NpcsLaunchPlugin",
                "FMLCorePluginContainsFMLMod": "true",
                "FMLAT"                      : "deobfuscation.cfg",
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"               : "mixins.npcs.json",
                "ForceLoadAsMod"             : "true"
        ])
    }
}

jar.finalizedBy('reobfJar')

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}
subprojects {
    apply plugin: 'idea'
}

task prepareAssets(type: Copy) {
    group = 'build'
    from project.file('src/main/resources')
    into project.file('build/classes/java/main')
}

classes.dependsOn(prepareAssets)